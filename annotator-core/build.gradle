/*
 * MIT License
 *
 * Copyright (c) 2020 Nima Karimipour
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */

import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'maven-publish'
    id 'application'
    id "com.vanniktech.maven.publish"
}

application {
    mainClass = 'edu.ucr.cs.riple.core.Main'
}

dependencies {
    implementation project(':injector')
    implementation project(':annotator-scanner')
    implementation project(':annotator-util')
    implementation deps.build.guava
    implementation deps.build.gson
    implementation deps.build.progressbar
    implementation deps.build.javaparser
    implementation deps.build.commonscli
    implementation deps.build.log4japi
    implementation deps.build.log4jcore
    implementation deps.build.jgitCore
    implementation deps.build.jgitSsh
    implementation deps.build.sqlite

    testImplementation deps.build.commonsio
    testImplementation deps.test.junit
    testImplementation deps.test.mockito
}

// Exclude formatting files under resources
spotless {
    java {
        targetExclude 'src/test/resources/**/*.java'
    }
}

// Should be the latest supporting version of NullAway.
def NULLAWAY_TEST = "0.12.4-SNAPSHOT"

tasks.test.dependsOn(':annotator-scanner:publishToMavenLocal')
tasks.test.dependsOn(':annotator-util:publishToMavenLocal')
tasks.test.dependsOn(':annotation-util:publishToMavenLocal')

// Set up environment variables for test configuration tu run with the latest development version.
tasks.test.doFirst {
    environment "NULLAWAY_TEST_VERSION", NULLAWAY_TEST
    environment "ANNOTATOR_VERSION", project.version
    if (!System.getenv().containsKey("OPENAI_KEY")) {
        environment "OPENAI_KEY", "OPENAI_KEY_FOR_TEST"
    }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }

    mavenPublishing {
        publishToMavenCentral(SonatypeHost.S01)
    }

    repositories {
        mavenLocal()
    }
}

jar {
    // add this classifier to prevent overwriting jar and shadowJar outputs to preserve incremental build of gradle.
    archiveClassifier = "nonshadow"
}

// To remove the "-all" postfix from the produced jar file.
shadowJar {
    archiveClassifier = null
}

// Exclude tests not supported by Java 11.
if (JavaVersion.current().isJava11()) {
    // exclude Java17Test which is designed to test Java 17 features.
    test {
        filter {
            excludeTestsMatching "edu.ucr.cs.riple.core.Java17Test"
            // temporarily exclude LombokTest until we find a solution for the issue.
            excludeTestsMatching "edu.ucr.cs.riple.core.LombokTest"
        }
    }
}
